buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "se.transmode.gradle:gradle-docker:1.2"
    }
}

description = "Use Spring Boot with Tapestry"
group = "info.code8"
version = "0.1.0-SNAPSHOT"
ext {
    versions =  [
            spring:     '5.0.8.RELEASE',
            boot:       '2.0.4.RELEASE',
            tapestry:   '5.4.3',
            junit:      '4.12'
    ]
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'

jar {
    baseName = 'tapestry-boot'
    archivesBaseName = 'tapestry-boot'
    manifest { attributes 'Tapestry-Module-Classes': 'info.code8.tapestry.AssetSourceModule' }
    // Set true if only jar package and not spring boot executable jar
    enabled = true
}

bootJar {
    // workaround for 'Unable to find main class'
    mainClassName = '...'
}

task sourceJar(type: Jar) {
  dependsOn classes
  classifier "sources"
  from sourceSets.main.allSource
}

artifacts {
  archives sourceJar
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.springframework.boot:spring-boot-autoconfigure:${versions.boot}"
    compile "org.springframework:spring-web:${versions.spring}"
    compile "org.apache.tapestry:tapestry-core:${versions.tapestry}"
    compile "javax.servlet:javax.servlet-api:3.1.0"

    testCompile "junit:junit:${versions.junit}"
    testCompile 'org.springframework.boot:spring-boot-starter-web'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}
